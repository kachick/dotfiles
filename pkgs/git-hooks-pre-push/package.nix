{
  pkgs,
  lib,
  makeWrapper,
  ...
}:
pkgs.unstable.buildGo124Module (finalAttrs: {
  pname = "git-hooks-pre-push";
  version = "0.0.1";

  nativeBuildInputs = [
    makeWrapper
  ];

  wrapperPath = lib.makeBinPath (
    with pkgs;
    [
      git
      typos
      unstable.gitleaks
      my.run_local_hook
    ]
  );

  postFixup = ''
    wrapProgram $out/bin/${finalAttrs.meta.mainProgram} \
      --prefix PATH : "${finalAttrs.wrapperPath}"
  '';

  vendorHash = "sha256-2EKPmpup+endoiOA7nsS6LCQs7RN8WiII839EBjinqc=";
  src =
    with lib.fileset;
    toSource rec {
      root = ../../.;
      # Don't just use `fileset.gitTracked root`, then always rebuild even if just changed the README.md
      fileset = intersection (gitTracked root) (unions [
        ../../go.mod
        ../../go.sum
        ../../internal
        ./.
      ]);
    };

  subPackages = [
    "pkgs/${finalAttrs.pname}"
  ];

  env.CGO_ENABLED = 0;

  ldflags = [
    "-s"
    "-w"
    "-X main.TyposConfigPath=${../../typos.toml}"
  ];

  meta = {
    description = "GH-540 and GH-699";
    mainProgram = finalAttrs.pname;
  };
})
